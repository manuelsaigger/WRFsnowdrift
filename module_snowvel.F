MODULE SNOWVEL

!-------------------------------------------------------------------
! Module to calculate the snow settling velocity
! and the particle radius
!-------------------------------------------------------------------

CONTAINS
SUBROUTINE snow_vel(config_flags, dz8w, rho_phy, rmean, zmid, zlow, g, setvel, ustarcor, &
                    ids, ide, jds, jde, kds, kde,                     &
                    ims, ime, jms, jme, kms, kme,                     &
                    its, ite, jts, jte, kts, kte)
 USE module_configure             !frame/
 USE module_state_description     !frame/ --> generated by registry

 IMPLICIT NONE
 TYPE(grid_config_rec_type),  INTENT(IN   ) :: config_flags
 INTEGER,INTENT(IN) :: ids,ide, jds,jde, kds,kde,               &
                       ims,ime, jms,jme, kms,kme,               &
                       its,ite, jts,jte, kts,kte 
 REAL,DIMENSION(ims:ime,kms:kme,jms:jme), INTENT(IN) :: dz8w, zmid, zlow, rho_phy
 REAL,DIMENSION(ims:ime,kms:kme,jms:jme), INTENT(OUT) :: setvel, rmean
 REAL,DIMENSION(ims:ime,jms:jme), INTENT(IN) :: ustarcor
 REAL,INTENT(IN) :: g
 INTEGER         :: i, k, j

!Local variables
 REAL           :: rhoi, nue, ka, C, B, rground

!---------------VARIABLES-------------------------------------------------
 !zmid: topographic height at the middle of the layer [m]
 !zlow: topographic height at the lower boundary of layer k [m]
 !rmean: particle radius (changes with height) [m]
!------------------------------------------------------------------------
!---------------CONSTANTS------------------------------------------------
 rhoi = 900    !ice density [kg/m³]
 nue = 1.2E-5  !kinematic viscosity of air [m²/s]  
!-----------------------------------------------------------------------

   DO j=jts,jte
    DO i=its,ite

    !if particle radius is not set in the namelist calculate it (for the lowest model level)
    if (config_flags%rgro == 0.0) then
    !calculate radius as a function of friction velocity
    !may not be suitable for high (> 15 m/s) or low (< 6 m/s) 10m-wind speeds
    rground = ((7.8E-6 * ustarcor(i,j)) / 0.036 + 31E-6 ) / 2  !Schmid 2021, equation (32)
    else  !else take constant radius that was set in the namelist
    rground = config_flags%rgro
    endif

     DO k=kts,kte

     !change of particle radius with height, Schmid 2021, equation (33)
     rmean(i,k,j) = rground *(zmid(i,k,j)-zlow(i,kts,j))**(-0.258)

     !if settling velocity is not set in the namelist calculate it (for the lowest model level)
     if (config_flags%pavel==0.0) then  !Schmid 2021, equation (39)-(41)
      C = (6.203*nue)/2
      B = (5.516 * rhoi * g) / (4 * rho_phy(i,k,j))
      setvel(i,k,j) = MAX(1E-9, (-C/rmean(i,k,j)) + sqrt((C/rmean(i,k,j))**2 + B*rmean(i,k,j)))
     else   !else take settling velocity that was set in the namelist
      setvel(i,k,j) = config_flags%pavel
     endif

     ENDDO    !end k
    ENDDO    !end i
   ENDDO    !end j


END SUBROUTINE snow_vel
END MODULE SNOWVEL
